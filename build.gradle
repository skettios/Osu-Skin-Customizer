apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.6
sourceSets.main.java.srcDirs = [ "src/" ]

ext.buildNumber = System.getenv("BUILD_NUMBER") ?: "dev"
ext.mainClassName = "com.skettios.osc.Start"
ext.assetDir = "assets"
version = "3.0.${buildNumber}"

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-io:commons-io:2.4'
    compile 'com.google.code.gson:gson:2.3.1'
}

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
	workingDir = project.assetDir
    standardInput = System.in
    ignoreExitValue = true
}

task compile(type: Jar) {
	delete("build/libs")
	from files(sourceSets.main.output.classesDir)
	from files(sourceSets.main.output.resourcesDir)
	from { configurations.compile.collect { zipTree(it) } }

	archiveName = "Osu-Skin-Customizer-${version}.jar"

	manifest {
		attributes 'Main-Class': project.mainClassName
	}
}

task dist(type: Zip) {
	delete("build/distributions")
	from 'build/libs'
	from 'assets'
}

build.dependsOn compile, dist

eclipse {
    project {
        name = "Osu-Skin-Customizer"
    }
}

task afterEclipseImport(description: "Post processing after project generation", group: "IDE") {
    doLast {
        def classpath = new XmlParser().parse(file(".classpath"))
        new Node(classpath, "classpathentry", [ kind: 'src', path: 'assets' ]);
        def writer = new FileWriter(file(".classpath"))
        def printer = new XmlNodePrinter(new PrintWriter(writer))
        printer.setPreserveWhitespace(true)
        printer.print(classpath)
    }
}